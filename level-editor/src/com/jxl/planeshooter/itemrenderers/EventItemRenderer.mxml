<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				currentState="hidden"
				autoDrawBackground="false"
				creationComplete="init();">
	
	<fx:Metadata>
		[Event(name="editMovie", type="com.jxl.planeshooter.events.EventItemRendererEvent")]
		[Event(name="deleteItem", type="com.jxl.planeshooter.events.EventItemRendererEvent")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import com.jxl.planeshooter.constants.EnemyTypes;
			import com.jxl.planeshooter.events.EventItemRendererEvent;
			import com.jxl.planeshooter.views.MovieEditor;
			import com.jxl.planeshooter.vo.EnemyVO;
			import com.jxl.planeshooter.vo.MovieVO;
			
			import mx.collections.ArrayCollection;
			
			
			[Bindable]
			public var enemy:EnemyVO;
			
			[Bindable]
			public var movie:MovieVO;
			
			[Bindable]
			public var enemyTypes:ArrayCollection;
			
			[Bindable]
			public var enemyIcon:*;
			
			private function init():void
			{
				enemyTypes = new ArrayCollection([
					EnemyTypes.PLANE,
					EnemyTypes.MISSILE,
					EnemyTypes.JET,
					EnemyTypes.BOMBER,
					EnemyTypes.UFO
					
					]);
			}
			
			public override function set data(value:Object):void
			{
				super.data = value;
				
				enemy = null;
				movie = null;
				
				if(value && value is EnemyVO)
				{
					enemy = value as EnemyVO;
					currentState = "enemy";
					updateIcon();
				}
				else if(value && value is MovieVO)
				{
					movie = value as MovieVO;
					currentState = "movie";
				}
				else
				{
					currentState = "hidden";
				}
			}
			
			private function onTimeChange():void
			{
				var seconds:Number = timeNumericStepper.value;
				var minutes:Number = seconds / 60;
				timeLabel.text = Math.floor(minutes) + " minutes, " + Math.floor(seconds) + " seconds";
				data.when = timeNumericStepper.value;
			}
			
			private function onEditMovie():void
			{
				var evt:EventItemRendererEvent = new EventItemRendererEvent(EventItemRendererEvent.EDIT_MOVIE);
				evt.movie = movie;
				dispatchEvent(evt);
			}
			
			private function onUpdateEnemyIcon():void
			{
				//Debug.debug("enemy.type: " + enemy.type);
				enemy.type = enemyComboBox.selectedItem;
				updateIcon();
			}
			
			private function updateIcon():void
			{
				enemyIcon = "assets/images/" + enemy.type + ".png";
			}
			
			private function onDelete():void
			{
				var evt:EventItemRendererEvent = new EventItemRendererEvent(EventItemRendererEvent.DELETE_ITEM);
				evt.item = data;
				dispatchEvent(evt);
			}
			
			private function onPause():void
			{
				data.pause = pauseCheckBox.selected;
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:NumberFormatter id="numFormatter" fractionalDigits="1" />
	</fx:Declarations>
	
	<s:states>
		<s:State name="hidden" />
		<s:State name="enemy" />
		<s:State name="movie" />
	</s:states>
	
	<s:Rect width="100%" height="100%">
		<s:fill>
			<s:SolidColor color="#EEEEEE" />
		</s:fill>
		<s:stroke>
			<s:SolidColorStroke color="#333333" />
		</s:stroke>
	</s:Rect>
	
	<s:HGroup verticalAlign="middle" gap="4" paddingBottom="4" paddingTop="4" paddingLeft="4" paddingRight="4">
		<s:Label text="Enemy" fontWeight="bold" includeIn="enemy" />
		<s:Label text="Movie" fontWeight="bold" includeIn="movie" />
		<s:Label text="Time:" />
		<s:NumericStepper id="timeNumericStepper" minimum="0" maximum="300" 
						  value="{data.when}"
						  change="onTimeChange()" />
		<s:Label id="timeLabel" />
		<s:Label text="Enemy:"  includeIn="enemy" />
		<s:ComboBox id="enemyComboBox" selectedItem="{enemy.type}" dataProvider="{enemyTypes}" change="onUpdateEnemyIcon()" includeIn="enemy" />
		<s:CheckBox id="pauseCheckBox" label="Pause:" selected="false" includeIn="enemy" click="onPause()" />
		<s:Image width="22" height="22" id="enemyIconImage" source="{enemyIcon}" includeIn="enemy" />
		<mx:LinkButton label="Edit" click="onEditMovie()" includeIn="movie" />
		<s:Button label="X" width="40" click="onDelete()" />
	</s:HGroup>
	
</s:ItemRenderer>
